group 'org.yetiz.utils'
version '2.1.0.0.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "hbase-utils"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "https://repository.cloudera.com/artifactory/cloudera-repos/"
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // CDH HBase
//    compile 'org.apache.hbase:hbase-client:1.0.0-cdh5.5.1'
    // Apache
    compile 'org.apache.hbase:hbase-client:1.0.0'
    // for Logging - start
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.3'
    compile 'org.apache.logging.log4j:log4j-api:2.3'
    compile 'org.apache.logging.log4j:log4j-core:2.3'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.6.1'
    // reflection
    compile 'org.javassist:javassist:3.20.0-GA'
    compile 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.reflections:0.9.10_3'

}

buildscript {
    repositories {
        mavenCentral()
    }
}

//def manifestMainClass = 'Launcher'
def manifestImplementationVersion = "${version}"
def manifestBuiltDate = System.currentTimeMillis()
//def manifestClassPath = configurations.compile.collect { "libs${File.separator}${it.getName()}" }.join(' ')

task library(type: Copy) {
    into "$buildDir${File.separator}output${File.separator}libs"
    from configurations.runtime
}

task copyLicense(type: Copy) {
    into sourceSets.main.output.resourcesDir
    from "$projectDir${File.separator}LICENSE"
}

task copyNotice(type: Copy) {
    into sourceSets.main.output.resourcesDir
    from "$projectDir${File.separator}NOTICE"
}

jar {
    dependsOn library
    dependsOn copyLicense
    dependsOn copyNotice
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    destinationDir file("$buildDir${File.separator}output/")
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    destinationDir file("$buildDir${File.separator}output/")
    from sourceSets.main.allSource
}

task manifastJar(type: Jar) {
    archiveName = "$baseName-$version.$extension"
    destinationDir file("$buildDir${File.separator}output/")
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    manifest {
        attributes 'Implementation-Version': manifestImplementationVersion,
                'Built-Date': manifestBuiltDate
    }
}

artifacts {
    archives javadocJar, sourcesJar, manifastJar
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                description 'This is a open project for HBase operation, define a dao model and wrap HBase operation. Create a easy way to do batch operation and other admin command.'
                url 'https://github.com/yetiz-org/hbase-utils'

                scm {
                    url 'scm:git@github.com:yetiz-org/hbase-utils.git'
                    connection 'scm:git@github.com:yetiz-org/hbase-utils.git'
                    developerConnection 'scm:git@github.com:yetiz-org/hbase-utils.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'yetisno'
                        name 'Yeti Sno'
                        email 'yeti@yetiz.org'
                    }
                }
            }
        }
    }

    signing {
//        sign configurations.archives
    }

}